plugins {
  id 'java'
  id 'idea'
}

group = 'com.dfbnc'

repositories {
    mavenCentral()
    maven { url 'https://artifactory.dmdirc.com/artifactory/repo' }
}

idea {
    module {
         sourceDirs += file('modules/parser/irc/src/main/generated')
         sourceDirs += file('modules/parser/common/src/main/generated')
         testSourceDirs += file('modules/parser/irc/src/test/generated_tests')
         testSourceDirs += file('modules/parser/common/src/test/generated_tests')

         generatedSourceDirs = [file('modules/parser/irc/src/main/generated'),
                                file('modules/parser/common/src/main/generated'),
                                file('modules/parser/irc/src/test/generated_tests'),
                                file('modules/parser/common/src/test/generated_tests')]
    }
}

configurations {
    bundle
    compile {
        extendsFrom bundle
    }
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

sourceSets {
    main.java.srcDirs = ['src',
                         'modules/sockets/src',
                         'modules/parser/irc/src/main/java',
                         'modules/parser/common/src/main/java']
    test.java.srcDirs = ['test']
}

dependencies {
    compile group: 'com.squareup.dagger', name: 'dagger-compiler', version: '1.2.5'
    compile group: 'com.google.auto.value', name: 'auto-value', version: '1.3'

    bundle group: 'org.kohsuke', name: 'akuma', version: '1.10'
    bundle group: 'com.squareup.dagger', name: 'dagger', version: '1.2.5'
    bundle group: 'com.google.guava', name:'guava', version: '19.0'

    bundle group: 'net.engio', name: 'mbassador', version: '1.3.0'
    bundle group: 'de.odysseus.juel', name: 'juel-api', version: '2.2.7'
    bundle group: 'de.odysseus.juel', name: 'juel-impl', version: '2.2.7'
    bundle group: 'de.odysseus.juel', name: 'juel-spi', version: '2.2.7'

    bundle group: 'com.dmdirc', name: 'util', version: 'null-SNAPSHOT'

    testCompile group: 'junit', name: 'junit', version: '4.12'
    testCompile group: 'org.mockito', name: 'mockito-core', version: '2.2.7'
}

apply from: 'gradle/tests.gradle'
apply from: 'gradle/jar.gradle'

task wrapper(type: Wrapper) {
    gradleVersion = '3.1'
}
