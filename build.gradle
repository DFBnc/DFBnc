plugins {
  id 'java'
  id 'idea'
}

group = 'com.dfbnc'

repositories {
    mavenCentral()
    maven { url 'https://artifactory.dmdirc.com/snapshots' }
    maven { url 'https://maven.shanemcc.net/releases' }
}

idea {
    module {
        if (file('modules/parser').exists()) {
            sourceDirs += file('modules/parser/irc/src/main/generated')
            sourceDirs += file('modules/parser/common/src/main/generated')
            testSourceDirs += file('modules/parser/irc/src/test/generated_tests')
            testSourceDirs += file('modules/parser/common/src/test/generated_tests')

            generatedSourceDirs = [file('modules/parser/irc/src/main/generated'),
                                    file('modules/parser/common/src/main/generated'),
                                    file('modules/parser/irc/src/test/generated_tests'),
                                    file('modules/parser/common/src/test/generated_tests')]
        }
    }
}

configurations {
    bundle
    compile {
        extendsFrom bundle
    }
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

sourceSets {
    main.java.srcDirs = ['src']
    main.resources.srcDirs = ['res']
    test.java.srcDirs = ['test']
}

dependencies {
    bundle group: 'uk.org.dataforce.libs', name: 'logger', version: '0.4.1'
    bundle group: 'uk.org.dataforce.libs', name: 'cliparser', version: '0.2.1'

    bundle group: 'org.kohsuke', name: 'akuma', version: '1.10'
    bundle group: 'com.dmdirc', name: 'util', version: 'null-SNAPSHOT'

    if (file('modules/sockets/build.gradle').exists()) {
        bundle project(":sockets")
    } else {
        bundle group: 'com.dfbnc', name: 'sockets', version: '0.3.6'
    }

    if (file('modules/parser/build.gradle').exists()) {
        bundle project(":parser:common")
        bundle project(":parser:irc")
    } else {
        bundle group: 'net.engio', name: 'mbassador', version: '1.3.2'
        bundle group: 'com.dmdirc', name: 'parser-irc', version: '0.8-366-g2bb768a-SNAPSHOT'
        bundle group: 'com.dmdirc', name: 'parser-common', version: '0.8-351-g3a87922-SNAPSHOT'
    }

    testCompile group: 'junit', name: 'junit', version: '4.13.2'
    testCompile group: 'org.mockito', name: 'mockito-core', version: '4.2.0'
}

apply from: 'gradle/tests.gradle'
apply from: 'gradle/jar.gradle'

task wrapper(type: Wrapper) {
    gradleVersion = '3.3'
}
